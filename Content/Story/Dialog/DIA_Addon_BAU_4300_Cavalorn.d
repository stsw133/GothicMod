//******************************************************************************************
//	Info EXIT
//******************************************************************************************
INSTANCE DIA_Addon_Cavalorn_EXIT (C_INFO)
{
	npc									=	Bau_4300_Addon_Cavalorn;
	nr									=	999;
	condition							=	atrue;
	information							=	DIA_Addon_Cavalorn_EXIT_Info;
	permanent 							=	true;
	description							=	DIALOG_ENDE;
};

FUNC VOID DIA_Addon_Cavalorn_EXIT_Info()
{
	AI_StopProcessInfos(self);
};

///////////////////////////////////////////////////////////////////////
//	C_BragoBanditsDead
///////////////////////////////////////////////////////////////////////
func int C_BragoBanditsDead()
{
	if (Npc_IsDead(Ambusher_1013) || (Bdt_1013_Away == true))
	&& (Npc_IsDead(Ambusher_1014))
	&& (Npc_IsDead(Ambusher_1015))
	{
		return true;
	};
};

//******************************************************************************************
//	Info MeetingIsRunning
//******************************************************************************************
instance DIA_Addon_Cavalorn_MeetingIsRunning (C_INFO)
{
	npc									=	Bau_4300_Addon_Cavalorn;
	nr									=	1;
	condition							=	DIA_Addon_Cavalorn_MeetingIsRunning_Condition;
	information							=	DIA_Addon_Cavalorn_MeetingIsRunning_Info;
	important							=	true;
};

func int DIA_Addon_Cavalorn_MeetingIsRunning_Condition()
{
	if (Npc_IsInState(self,ZS_Talk))
	&& (RangerMeetingRunning == LOG_RUNNING)
	{
		return true;
	};
};

func void DIA_Addon_Cavalorn_MeetingIsRunning_Info()
{
	AI_Output (self, other, "DIA_Addon_Cavalorn_MeetingIsRunning_08_00"); //Witaj w Krêgu, przyjacielu.
};

//******************************************************************************************
//	Info Hallo
//******************************************************************************************
instance DIA_Addon_Cavalorn_HALLO (C_INFO)
{
	npc									=	Bau_4300_Addon_Cavalorn;
	nr									=	1;
	condition							=	DIA_Addon_Cavalorn_HALLO_Condition;
	information							=	DIA_Addon_Cavalorn_HALLO_Info;
	important							=	true;
};

func int DIA_Addon_Cavalorn_HALLO_Condition()
{
	if (Npc_IsInState(self,ZS_Talk))
	{
		return true;
	};
};

func void DIA_Addon_Cavalorn_HALLO_Info ()
{
	AI_Output (other, self, "DIA_Addon_Cavalorn_HALLO_15_00"); //Jakieœ k³opoty?
	AI_Output (self, other, "DIA_Addon_Cavalorn_HALLO_08_01"); //Do diab³a. Nie wiem, gdzie siê ukrywaj¹. Zabijesz jednego, ale zaraz wracaj¹.
	AI_Output (self, other, "DIA_Addon_Cavalorn_HALLO_08_02"); //Chwila. Ja ciebie znam. To ty ci¹gle ¿ebra³eœ u mnie o strza³y, jeszcze w Górniczej Dolinie.
	AI_Output (other, self, "DIA_Addon_Cavalorn_HALLO_Ja_15_00"); //Nazywasz siê Cavalorn, tak?
	AI_Output (self, other, "DIA_Addon_Cavalorn_HALLO_Ja_08_01"); //Ach, nie zapomnia³eœ o mnie. Koniec koñców obaj przeszliœmy przez tê przeklêt¹ koloniê.
	AI_Output (self, other, "DIA_Addon_Cavalorn_HALLO_Ja_08_02"); //Dok¹d idziesz?

	Info_ClearChoices(DIA_Addon_Cavalorn_HALLO);
	Info_AddChoice (DIA_Addon_Cavalorn_HALLO, "Nie mam okreœlonego celu.", DIA_Addon_Cavalorn_HALLO_keinZiel);
	Info_AddChoice (DIA_Addon_Cavalorn_HALLO, "Z powrotem do Górniczej Doliny.", DIA_Addon_Cavalorn_HALLO_Bauern);
	Info_AddChoice (DIA_Addon_Cavalorn_HALLO, "Do miasta.", DIA_Addon_Cavalorn_HALLO_Stadt);
};
func void DIA_Addon_Cavalorn_HALLO_Stadt()
{
	AI_Output (other, self, "DIA_Addon_Cavalorn_HALLO_Stadt_15_00"); //Do miasta.
	AI_Output (self, other, "DIA_Addon_Cavalorn_HALLO_Stadt_08_01"); //Ach, te¿ do miasta.
	if (Mil_310_Stadtwache.aivar[AIV_Guardpassage_Status] != GP_PassGate)
	{
		AI_Output (self, other, "DIA_Addon_Cavalorn_HALLO_Stadt_08_02"); //Mia³byœ k³opoty ze stra¿¹. Teraz, gdy a¿ roi siê tu od bandytów, nie ka¿dy mo¿e wejœæ do miasta.
		AI_Output (self, other, "DIA_Addon_Cavalorn_HALLO_Stadt_08_03"); //Kilka dni temu przechodzi³ têdy jeden ze starych Psioników z Górniczej Doliny. Mówi³, ¿e mo¿e wchodziæ i wychodziæ z Khorinis, kiedy tylko chce.
		AI_Output (self, other, "DIA_Addon_Cavalorn_HALLO_Stadt_08_04"); //Poszed³ do doliny pod t¹ du¿¹ wie¿¹. Gdzieœ w okolicy wodospadu musi byæ przejœcie.
		AI_Output (self, other, "DIA_Addon_Cavalorn_HALLO_Stadt_08_05"); //Mo¿e powinieneœ z nim porozmawiaæ.
	};
	Info_ClearChoices(DIA_Addon_Cavalorn_HALLO);
};

func void DIA_Addon_Cavalorn_HALLO_Bauern()
{
	AI_Output (other, self, "DIA_Addon_Cavalorn_HALLO_Bauern_15_00"); //Z powrotem do Górniczej Doliny.
	AI_Output (self, other, "DIA_Addon_Cavalorn_HALLO_Bauern_08_01"); //Naprawdê? Hmm. Szkoda, ¿e nie mogê z tob¹ iœæ, ale muszê za³atwiæ kilka spraw.
	AI_Output (self, other, "DIA_Addon_Cavalorn_HALLO_Bauern_08_02"); //Je¿eli dostaniesz siê do Górniczej Doliny, sprawdŸ, czy moja chata jeszcze stoi, dobrze? Pewnego dnia chcia³bym tam wróciæ.

	QuestStep_CavalornHut = SetQuestStatus (Quest_CavalornHut, LOG_RUNNING, QuestStep_CavalornHut);
	SetNoteEntry (Quest_CavalornHut, LOG_MISSION, "Cavalorn chce, ¿ebym sprawdzi³, czy w Górniczej Dolinie wci¹¿ stoi jego stara chata. Z tego, co pamiêtam, znajdowa³a siê ona na wzgórzach na zachód od Starego Obozu. Wydaje mi siê, ¿e coœ tam mog³o zostaæ.");

	Info_ClearChoices(DIA_Addon_Cavalorn_HALLO);
};

func void DIA_Addon_Cavalorn_HALLO_keinZiel()
{
	AI_Output (other, self, "DIA_Addon_Cavalorn_HALLO_keinZiel_15_00"); //Nie mam okreœlonego celu.
	AI_Output (self, other, "DIA_Addon_Cavalorn_HALLO_keinZiel_08_01"); //Nie chcesz powiedzieæ, co? Nie ma sprawy.
	Info_ClearChoices(DIA_Addon_Cavalorn_HALLO);
};

//******************************************************************************************
//	Info Beutel
//******************************************************************************************
instance DIA_Addon_Cavalorn_Beutel (C_INFO)
{
	npc									=	Bau_4300_Addon_Cavalorn;
	nr									=	2;
	condition							=	DIA_Addon_Cavalorn_Beutel_Condition;
	information							=	DIA_Addon_Cavalorn_Beutel_Info;
	description							=	"By³em w twojej chacie w Górniczej Dolinie.";
};

func int DIA_Addon_Cavalorn_Beutel_Condition()
{
	if (Npc_KnowsInfo(other,DIA_Addon_Cavalorn_HALLO))
	&& (Npc_HasItems(other,ItSe_CavalornsBeutel))
	{
		return true;
	};
};

func void DIA_Addon_Cavalorn_Beutel_Info()
{
	AI_Output (other, self, "DIA_Addon_Cavalorn_Beutel_15_00"); //By³em w twojej chacie w Górniczej Dolinie.
	AI_Output (self, other, "DIA_Addon_Cavalorn_Beutel_08_01"); //A zatem jeszcze stoi.
	AI_Output (self, other, "DIA_Addon_Cavalorn_Beutel_08_03"); //Uda³o ci siê to znaleŸæ?
	AI_Output (other, self, "DIA_Addon_Cavalorn_Beutel_15_04"); //Je¿eli mówisz o torbie pe³nej rudy... Tak, mam j¹.
	AI_Output (self, other, "DIA_Addon_Cavalorn_Beutel_08_05"); //Ale z ciebie spryciula, wiesz o tym?
	AI_Output (self, other, "DIA_Addon_Cavalorn_Beutel_08_07"); //Masz j¹ ze sob¹? Dam ci za ni¹ 100 sztuk z³ota.
	AI_Output (other, self, "DIA_Addon_Cavalorn_Beutel_ja_15_00"); //Pewnie.
	AI_Output (self, other, "DIA_Addon_Cavalorn_Beutel_ja_08_01"); //Œwietnie.

	B_GiveInvItems (self, other, ItMi_Gold, 100);
	B_GiveInvItems (other, self, ItSe_CavalornsBeutel, 1);
	B_GivePlayerXP(XP_BONUS_5);

	AI_Output (other, self, "DIA_Addon_Cavalorn_Beutel_why_15_00"); //A dlaczego j¹ zostawi³eœ?
	AI_Output (self, other, "DIA_Addon_Cavalorn_Beutel_why_08_01"); //Kto móg³by przypuszczaæ, ¿e w Khorinis ruda jest tak cenna.
	AI_Output (self, other, "DIA_Addon_Cavalorn_Beutel_why_08_02"); //W kolonii za g³upi kawa³ek rudy nie dosta³byœ nawet miejsca do spania.

	QuestStep_CavalornHut = SetQuestStatus (Quest_CavalornHut, LOG_SUCCESS, QuestStep_CavalornHut);
};

//******************************************************************************************
//	Info WasMachstdu
//******************************************************************************************
instance DIA_Addon_Cavalorn_WASMACHSTDU (C_INFO)
{
	npc									=	Bau_4300_Addon_Cavalorn;
	nr									=	3;
	condition							=	DIA_Addon_Cavalorn_WASMACHSTDU_Condition;
	information							=	DIA_Addon_Cavalorn_WASMACHSTDU_Info;
	description							=	"Co tu robisz?";
};

func int DIA_Addon_Cavalorn_WASMACHSTDU_Condition()
{
	if (Npc_KnowsInfo(other,DIA_Addon_Cavalorn_HALLO))
	&& (MIS_Addon_Nefarius_BringMissingOrnaments == 0)
	{
		return true;
	};
};

func void DIA_Addon_Cavalorn_WASMACHSTDU_Info()
{
	AI_Output (other, self, "DIA_Addon_Cavalorn_WASMACHSTDU_15_00"); //Co tu robisz?
	AI_Output (self, other, "DIA_Addon_Cavalorn_WASMACHSTDU_08_01"); //Utkn¹³em. Gdyby nie ci przeklêci bandyci, w ogóle by mnie tu nie by³o.
	AI_Output (self, other, "DIA_Addon_Cavalorn_Banditen_08_04"); //Cieszê siê, ¿e darowali mi ¿ycie. Nie uwa¿a³em i dosta³em w czaszkê.
	if (C_BragoBanditsDead() == false)
	{
		AI_Output (self, other, "DIA_Addon_Cavalorn_Banditen_08_05"); //Nie wiem, jak mam teraz odzyskaæ swoje rzeczy.
	};
};

//******************************************************************************************
//	Info ARTEFAKT
//******************************************************************************************
instance DIA_Addon_Cavalorn_ARTEFAKT (C_INFO)
{
	npc									=	BAU_4300_Addon_Cavalorn;
	nr									=	3;
	condition							=	DIA_Addon_Cavalorn_ARTEFAKT_Condition;
	information							=	DIA_Addon_Cavalorn_ARTEFAKT_Info;
	description							=	"Zosta³eœ obrabowany przez bandytów?";
};

func int DIA_Addon_Cavalorn_ARTEFAKT_Condition()
{
	if (Npc_KnowsInfo(other,DIA_Addon_Cavalorn_WASMACHSTDU))
	&& (QuestStep_KillBrago != LOG_SUCCESS)
	&& (MIS_Addon_Nefarius_BringMissingOrnaments == 0)
	&& (QuestStep_CavalornLetter != LOG_SUCCESS)
	{
		return true;
	};
};

func void DIA_Addon_Cavalorn_ARTEFAKT_Info()
{
	AI_Output (other, self, "DIA_Addon_Cavalorn_ARTEFAKT_15_00"); //Zosta³eœ obrabowany przez bandytów?
	AI_Output (self, other, "DIA_Addon_Cavalorn_ARTEFAKT_08_01"); //Tak. Og³uszyli mnie i zostawili jako ¿arcie dla goblinów.
	AI_Output (self, other, "DIA_Addon_Cavalorn_ARTEFAKT_08_02"); //To by³y piekielnie wa¿ne rzeczy. List i ca³a moja gotówka. Muszê je odzyskaæ.
	AI_Output (self, other, "DIA_Addon_Cavalorn_ARTEFAKT_08_03"); //Nie pójdê tam jednak bez wsparcia. Ta tchórzliwa ha³astra bandytów...
};

//******************************************************************************************
//	Info HELFEN
//******************************************************************************************
instance DIA_Addon_Cavalorn_HELFEN (C_INFO)
{
	npc									=	BAU_4300_Addon_Cavalorn;
	nr									=	3;
	condition							=	DIA_Addon_Cavalorn_HELFEN_Condition;
	information							=	DIA_Addon_Cavalorn_HELFEN_Info;
	description							=	"Mogê ci pomóc z tymi bandytami?";
};

func int DIA_Addon_Cavalorn_HELFEN_Condition()
{
	if (Npc_KnowsInfo(other,DIA_Addon_Cavalorn_ARTEFAKT))
	&& (MIS_Addon_Nefarius_BringMissingOrnaments == 0)
	&& (QuestStep_CavalornLetter != LOG_SUCCESS)
	&& (C_BragoBanditsDead() == false)
	{
		return true;
	};
};

func void DIA_Addon_Cavalorn_HELFEN_Info()
{
	AI_Output (other, self, "DIA_Addon_Cavalorn_HELFEN_15_00"); //Mogê ci pomóc z tymi bandytami?
	AI_Output (self, other, "DIA_Addon_Cavalorn_HELFEN_08_02"); //No có¿. Muszê chyba przyj¹æ twoj¹ ofertê. Nie mam zbyt wiele czasu.
	AI_Output (self, other, "DIA_Addon_Cavalorn_HELFEN_08_03"); //Dobra, s³uchaj. W dole tej œcie¿ki jest jedna z tych brudnych dziur, w których ukrywaj¹ siê bandyci.
	AI_Output (self, other, "DIA_Addon_Cavalorn_HELFEN_08_04"); //To te ³otry ukrad³y moje rzeczy.
	AI_Output (self, other, "DIA_Addon_Cavalorn_HELFEN_08_05"); //Daj znaæ, gdy bêdziesz gotów. Ruszymy za tymi szczurami!

	QuestStep_KillBrago = SetQuestStatus (Quest_KillBrago, LOG_RUNNING, QuestStep_KillBrago);
	SetNoteEntry (Quest_KillBrago, LOG_MISSION, "Bandyci ukradli coœ cennego Cavalornowi. Chce, ¿ebyœmy razem ich zabili.");
};

//******************************************************************************************
//	Info AUSRUESTUNG
//******************************************************************************************
instance DIA_Addon_Cavalorn_AUSRUESTUNG (C_INFO)
{
	npc									=	BAU_4300_Addon_Cavalorn;
	nr									=	8;
	condition							=	DIA_Addon_Cavalorn_AUSRUESTUNG_Condition;
	information							=	DIA_Addon_Cavalorn_AUSRUESTUNG_Info;
	description							=	"Potrzebujê lepszego sprzêtu.";
};

func int DIA_Addon_Cavalorn_AUSRUESTUNG_Condition()
{
	if (QuestStep_KillBrago != 0)
	&& (MIS_Addon_Nefarius_BringMissingOrnaments == 0)
	{
		return true;
	};
};

func void DIA_Addon_Cavalorn_AUSRUESTUNG_Info()
{
	AI_Output (other, self, "DIA_Addon_Cavalorn_AUSRUESTUNG_15_00"); //Potrzebujê lepszego sprzêtu.
	if (C_BragoBanditsDead() == false)
	{
		AI_Output (self, other, "DIA_Addon_Cavalorn_AUSRUESTUNG_08_01"); //Te œwinie nie zostawi³y mi zbyt wiele.
	};
	AI_Output (self, other, "DIA_Addon_Cavalorn_AUSRUESTUNG_08_02"); //Mogê ci daæ nó¿ na wilki. Wystarczy?
	AI_Output (other, self, "DIA_Addon_Cavalorn_AUSRUESTUNG_15_03"); //Tak, nó¿ bêdzie dobry.

	CreateInvItems (self, ItMw_1H_Sword_L_03, 1);
	B_GiveInvItems (self, other, ItMw_1H_Sword_L_03, 1);

	AI_Output (other, self, "DIA_Addon_Cavalorn_AUSRUESTUNG_15_04"); //A co z leczeniem?
	AI_Output (self, other, "DIA_Addon_Cavalorn_AUSRUESTUNG_08_05"); //Zosta³y mi dwie mikstury uzdrawiaj¹ce. Chcesz?
	AI_Output (other, self, "DIA_Addon_Cavalorn_AUSRUESTUNG_15_06"); //Pewnie. Wezmê je.

	CreateInvItems (self, ItPo_Health_01, 2);
	B_GiveInvItems (self, other, ItPo_Health_01, 2);
};

//******************************************************************************************
//	Info LETSKILLBANDITS
//******************************************************************************************
instance DIA_Addon_Cavalorn_LETSKILLBANDITS (C_INFO)
{
	npc									=	BAU_4300_Addon_Cavalorn;
	nr									=	9;
	condition							=	DIA_Addon_Cavalorn_LETSKILLBANDITS_Condition;
	information							=	DIA_Addon_Cavalorn_LETSKILLBANDITS_Info;
	description							=	"ChodŸmy zabiæ tych gnojków.";
};

func int DIA_Addon_Cavalorn_LETSKILLBANDITS_Condition()
{
	if (QuestStep_KillBrago == LOG_RUNNING)
	&& (MIS_Addon_Nefarius_BringMissingOrnaments == 0)
	&& (QuestStep_CavalornLetter != LOG_SUCCESS)
	&& (C_BragoBanditsDead() == false)
	{
		return true;
	};
};

func void DIA_Addon_Cavalorn_LETSKILLBANDITS_Info()
{
	AI_Output (other, self, "DIA_Addon_Cavalorn_LETSKILLBANDITS_15_00"); //ChodŸmy zabiæ tych gnojków.
	AI_Output (self, other, "DIA_Addon_Cavalorn_LETSKILLBANDITS_08_01"); //Dobra. Tylko mnie os³aniaj, jasne?
	AI_Output (self, other, "DIA_Addon_Cavalorn_LETSKILLBANDITS_08_02"); //A teraz czeka ich paskudna niespodzianka.

	Npc_ExchangeRoutine	(self, "KillBandits");
	self.aivar[AIV_PARTYMEMBER] = true;

	if (Bdt_1013_Away == false)
	{
		Ambusher_1013.aivar[AIV_EnemyOverride] = false;
	};
	Ambusher_1014.aivar[AIV_EnemyOverride] = false;
	Ambusher_1015.aivar[AIV_EnemyOverride] = false;

	AI_StopProcessInfos(self);
};

//******************************************************************************************
func void B_Addon_Cavalorn_VatrasBrief()
{
	B_GivePlayerXP(XP_BONUS_1);
	QuestStep_KillBrago = SetQuestStatus (Quest_KillBrago, LOG_SUCCESS, QuestStep_KillBrago);

	if (QuestStep_CavalornLetter != LOG_SUCCESS)
	{
		AI_Output (self, other, "DIA_Addon_Cavalorn_VatrasBrief_08_00"); //Przynajmniej mogê wykonaæ zadanie. I tak zmarnowa³em ju¿ zbyt wiele czasu.
		AI_Output (other, self, "DIA_Addon_Cavalorn_VatrasBrief_15_01"); //Jakie zadanie...?
		AI_Output (self, other, "DIA_Addon_Cavalorn_VatrasBrief_08_02"); //Ech. Muszê siê dostaæ do miasta, a póŸniej...
		AI_Output (self, other, "DIA_Addon_Cavalorn_VatrasBrief_08_03"); //Nie wiem tylko, jak mam zd¹¿yæ.
		AI_Output (other, self, "DIA_Addon_Cavalorn_VatrasBrief_15_04"); //A co ze mn¹?
		AI_Output (self, other, "DIA_Addon_Cavalorn_VatrasBrief_08_05"); //Hmm. Czemu nie. Móg³byœ zanieœæ list do miasta.
	};
	AI_Output (self, other, "DIA_Addon_Cavalorn_VatrasBrief_08_06"); //Dziêki temu bêdê mia³ czas, ¿eby pozbieraæ sprzêt.
	if (QuestStep_CavalornLetter != LOG_SUCCESS)
	{
		AI_Output (self, other, "DIA_Addon_Cavalorn_VatrasBrief_08_07"); //Jeden z tych bandytów powinien mieæ list w kieszeni.
		AI_Output (self, other, "DIA_Addon_Cavalorn_VatrasBrief_08_08"); //Zanieœ go do miasta i oddaj magowi Krêgu Wody, Vatrasowi. Znajdziesz go w œwi¹tyni Adanosa. Naucza tam ca³ymi dniami.
		AI_Output (self, other, "DIA_Addon_Cavalorn_VatrasBrief_08_09"); //Powiedz mu, ¿e nie zd¹¿y³em na czas.
		AI_Output (self, other, "DIA_Addon_Cavalorn_VatrasBrief_08_10"); //A gdyby ciê zapyta³, gdzie jestem, powiedz, ¿e w drodze na miejsce spotkania, dobrze?
		SetNoteEntry (Quest_WaterMages, LOG_MISSION, "Maga Wody, Vatrasa, mo¿na znaleŸæ w portowym mieœcie Khorinis. G³osi tam kazania w œwi¹tyni Adanosa.");
	};

	if (Npc_HasEquippedArmor(other) == false)
	{
		AI_Output (self, other, "DIA_Addon_Cavalorn_VatrasBrief_08_11"); //Aha, jeszcze jedno: kup sobie od jakiegoœ wieœniaka porz¹dne ubranie.
		AI_Output (self, other, "DIA_Addon_Cavalorn_VatrasBrief_08_12"); //Inaczej wezm¹ ciê za bandytê. Masz, tu jest parê monet.
		CreateInvItems (self, ItMi_Gold, 50);
		B_GiveInvItems (self, other, ItMi_Gold, 50);
	};

	QuestStep_CavalornLetter = SetQuestStatus (Quest_CavalornLetter, LOG_RUNNING, QuestStep_CavalornLetter);
	SetNoteEntry (Quest_CavalornLetter, LOG_MISSION, "Cavalorn chce, ¿ebym zaniós³ list magowi wody Vatrasowi");

	QuestStep_WaterMages = SetQuestStatus (Quest_WaterMages, LOG_RUNNING, QuestStep_WaterMages);

	Npc_ExchangeRoutine (self, "Start");
	self.aivar[AIV_PARTYMEMBER] = false;
};

//******************************************************************************************
//	Info BragoKilled
//******************************************************************************************
instance DIA_Addon_Cavalorn_BragoKilled (C_INFO)
{
	npc									=	BAU_4300_Addon_Cavalorn;
	nr									=	9;
	condition							=	DIA_Addon_Cavalorn_BragoKilled_Condition;
	information							=	DIA_Addon_Cavalorn_BragoKilled_Info;
	important							=	true;
};

func int DIA_Addon_Cavalorn_BragoKilled_Condition()
{
	if (Npc_GetDistToWP(self,"NW_XARDAS_BANDITS_LEFT") < 500)
	&& (QuestStep_KillBrago == LOG_RUNNING)
	&& (self.aivar[AIV_PartyMember] == true)
	&& (C_BragoBanditsDead() == true)
	{
		return true;
	};
};

func void DIA_Addon_Cavalorn_BragoKilled_Info()
{
	AI_Output (self, other, "DIA_Addon_Cavalorn_BragoKilled_08_00"); //No to za³atwione. Ech. Mogli ze mn¹ nie zaczynaæ.
	B_Addon_Cavalorn_VatrasBrief();
};

//******************************************************************************************
//	Info PCKilledBrago
//******************************************************************************************
instance DIA_Addon_Cavalorn_PCKilledBrago (C_INFO)
{
	npc									=	BAU_4300_Addon_Cavalorn;
	nr									=	9;
	condition							=	DIA_Addon_Cavalorn_PCKilledBrago_Condition;
	information							=	DIA_Addon_Cavalorn_PCKilledBrago_Info;
	description							=	"Bandyci s¹ ju¿ martwi.";
};

func int DIA_Addon_Cavalorn_PCKilledBrago_Condition()
{
	if (Npc_KnowsInfo(other,DIA_Addon_Cavalorn_WASMACHSTDU))
	&& (!Npc_KnowsInfo(other,DIA_Addon_Cavalorn_BragoKilled))
	&& (self.aivar[AIV_PartyMember] == false)
	&& (C_BragoBanditsDead() == true)
	{
		return true;
	};
};

func void DIA_Addon_Cavalorn_PCKilledBrago_Info()
{
	AI_Output (other, self, "DIA_Addon_Cavalorn_PCKilledBrago_15_00"); //Bandyci s¹ ju¿ martwi.
	B_Addon_Cavalorn_VatrasBrief();
};

//******************************************************************************************
//	Info JUNGS
//******************************************************************************************
instance DIA_Addon_Cavalorn_JUNGS (C_INFO)
{
	npc									=	BAU_4300_Addon_Cavalorn;
	nr									=	5;
	condition							=	DIA_Addon_Cavalorn_JUNGS_Condition;
	information							=	DIA_Addon_Cavalorn_JUNGS_Info;
	description							=	"Masz na sobie interesuj¹cy pancerz.";
};

func int DIA_Addon_Cavalorn_JUNGS_Condition()
{
	if (Npc_KnowsInfo(other,DIA_Addon_Cavalorn_HALLO))
	{
		return true;
	};
};

func void DIA_Addon_Cavalorn_JUNGS_Info()
{
	AI_Output (other, self, "DIA_Addon_Cavalorn_JUNGS_15_00"); //Masz na sobie interesuj¹cy pancerz. Nie nale¿ysz ju¿ do Cieni?
	AI_Output (self, other, "DIA_Addon_Cavalorn_JUNGS_08_01"); //Cieni? Od upadku bariery ju¿ nie istniej¹.
	AI_Output (self, other, "DIA_Addon_Cavalorn_JUNGS_08_02"); //Gdy tylko mogliœmy opuœciæ Górnicz¹ Dolinê, nie by³o powodu, ¿eby tam d³u¿ej zostawaæ.
	AI_Output (self, other, "DIA_Addon_Cavalorn_JUNGS_08_03"); //Teraz pracujê dla magów wody. Jestem cz³onkiem Wodnego Krêgu.

	SC_KnowsRanger = true;

	QuestStep_WaterRing = SetQuestStatus (Quest_WaterRing, LOG_RUNNING, QuestStep_WaterRing);
	SetNoteEntry (Quest_WaterRing, LOG_MISSION, "Cavalorn powiedzia³ mi, ¿e nale¿y do grupy zw¹cej siê Wodny Kr¹g.");

	Cavalorn_RangerHint = true;
};

//******************************************************************************************
//	Ring
//******************************************************************************************
instance DIA_Addon_Cavalorn_Ring (C_INFO)
{
	npc									=	BAU_4300_Addon_Cavalorn;
	nr									=	5;
	condition							=	DIA_Addon_Cavalorn_Ring_Condition;
	information							=	DIA_Addon_Cavalorn_Ring_Info;
	description							=	"Powiedz mi coœ wiêcej o Wodnym Krêgu!";
};

func int DIA_Addon_Cavalorn_Ring_Condition()
{
	if (Npc_KnowsInfo(other,DIA_Addon_Cavalorn_JUNGS))
	{
		return true;
	};
};

func void DIA_Addon_Cavalorn_Ring_Info()
{
	AI_Output (other, self, "DIA_Addon_Cavalorn_Ring_15_00"); //Powiedz mi coœ wiêcej o Wodnym Krêgu!
	AI_Output (self, other, "DIA_Addon_Cavalorn_Ring_08_01"); //Nie wolno mi o tym mówiæ.
	AI_Output (self, other, "DIA_Addon_Cavalorn_Ring_08_02"); //Mogê ciê jedynie wys³aæ do Vartasa. On reprezentuje Magów Wody w Khorinis.
	AI_Output (self, other, "DIA_Addon_Cavalorn_Ring_08_03"); //Najlepiej bêdzie, jak z nim porozmawiasz. Powiedz, ¿e ja ciê przys³a³em.
	AI_Output (self, other, "DIA_Addon_Cavalorn_Ring_08_04"); //Mo¿e pozwoli ci wst¹piæ do Krêgu. Bardzo potrzeba nam dobrych ludzi...

	SetNoteEntry (Quest_WaterRing, LOG_MISSION, "Vatras, Mag Wody, mo¿e powiedzieæ mi wiêcej o Wodnym Krêgu.");
};

//******************************************************************************************
//	Gegner
//******************************************************************************************
instance DIA_Addon_Cavalorn_Feinde (C_INFO)
{
	npc									=	BAU_4300_Addon_Cavalorn;
	nr									=	6;
	condition							=	DIA_Addon_Cavalorn_Feinde_Condition;
	information							=	DIA_Addon_Cavalorn_Feinde_Info;
	description							=	"Czy ty i twoi ludzie nie byliœcie czasem przeciwnikami Magów Wody?";
};

func int DIA_Addon_Cavalorn_Feinde_Condition()
{
	if (Npc_KnowsInfo(other,DIA_Addon_Cavalorn_JUNGS))
	{
		return true;
	};
};

func void DIA_Addon_Cavalorn_Feinde_Info()
{
	AI_Output (other, self, "DIA_Addon_Cavalorn_Feinde_15_00"); //Czy ty i twoi ludzie nie byliœcie czasem przeciwnikami Magów Wody?
	AI_Output (self, other, "DIA_Addon_Cavalorn_Feinde_08_01"); //Szalona przesz³oœæ jest ju¿ za nami. Nie ma ju¿ Nowego ani Starego Obozu.
	AI_Output (self, other, "DIA_Addon_Cavalorn_Feinde_08_02"); //A teraz, gdy nie ma ju¿ kolonii karnej, ka¿dy odpowiada za siebie.
	AI_Output (self, other, "DIA_Addon_Cavalorn_Feinde_08_03"); //Wiêkszoœæ z nas - by³ych wiêŸniów - nie zosta³a uniewinniona.
	AI_Output (self, other, "DIA_Addon_Cavalorn_Feinde_08_04"); //Magowie Wody zdo³ali uzyskaæ dla mnie u³askawienie i mogê teraz poruszaæ siê swobodnie.
};

//******************************************************************************************
//	Wo KdW
//******************************************************************************************
instance DIA_Addon_Cavalorn_KdWTask (C_INFO)
{
	npc									=	BAU_4300_Addon_Cavalorn;
	nr									=	7;
	condition							=	DIA_Addon_Cavalorn_KdWTask_Condition;
	information							=	DIA_Addon_Cavalorn_KdWTask_Info;
	description							=	"Co porabiaj¹ teraz Magowie Wody?";
};

func int DIA_Addon_Cavalorn_KdWTask_Condition()
{
	if (Npc_KnowsInfo(other,DIA_Addon_Cavalorn_JUNGS))
	&& (MIS_Addon_Nefarius_BringMissingOrnaments == 0)
	{
		return true;
	};
};

func void DIA_Addon_Cavalorn_KdWTask_Info()
{
	AI_Output (other, self, "DIA_Addon_Cavalorn_KdWTask_15_00"); //Co porabiaj¹ teraz Magowie Wody?
	AI_Output (self, other, "DIA_Addon_Cavalorn_KdWTask_08_01"); //Maj¹ wielki plan. Chodzi o niezbadan¹ czêœæ wyspy.
	AI_Output (other, self, "DIA_Addon_Cavalorn_KdWTask_15_02"); //Niezbadan¹ czêœæ? Gdzie ona jest?
	AI_Output (self, other, "DIA_Addon_Cavalorn_KdWTask_08_03"); //Nie mogê ci powiedzieæ nic wiêcej. Porozmawiaj z Vatrasem w Khorinis.
};	

//******************************************************************************************
//	Info BroughtLetter
//******************************************************************************************
instance DIA_Addon_Cavalorn_BroughtLetter (C_INFO)
{
	npc									=	BAU_4300_Addon_Cavalorn;
	nr									=	4;
	condition							=	DIA_Addon_Cavalorn_BroughtLetter_Condition;
	information							=	DIA_Addon_Cavalorn_BroughtLetter_Info;
	description							=	"Da³em Vatrasowi twój list.";
};

func int DIA_Addon_Cavalorn_BroughtLetter_Condition()
{
	if (QuestStep_CavalornLetter == LOG_SUCCESS)
	&& (Npc_KnowsInfo(other,DIA_Addon_Cavalorn_JUNGS))
	{
		return true;
	};
};

func void DIA_Addon_Cavalorn_BroughtLetter_Info()
{
	AI_Output (other, self, "DIA_Addon_Cavalorn_BroughtLetter_15_00"); //Da³em Vatrasowi twój list.
	AI_Output (self, other, "DIA_Addon_Cavalorn_BroughtLetter_08_01"); //Nie oczekiwa³em od ciebie niczego innego. Dziêkujê.
	B_GivePlayerXP(XP_BONUS_1);
};

//******************************************************************************************
//	Info Ornament
//******************************************************************************************
instance DIA_Addon_Cavalorn_Ornament (C_INFO)
{
	npc									=	Bau_4300_Addon_Cavalorn;
	nr									=	5;
	condition							=	DIA_Addon_Cavalorn_Ornament_Condition;
	information							=	DIA_Addon_Cavalorn_Ornament_Info;
	description							=	"Szukasz czegoœ?";
};

func int DIA_Addon_Cavalorn_Ornament_Condition()
{
	if (MIS_Addon_Nefarius_BringMissingOrnaments == LOG_RUNNING)
	&& (Npc_KnowsInfo(other,DIA_Addon_Cavalorn_HALLO))
	{
		return true;
	};
};

func void DIA_Addon_Cavalorn_Ornament_Info()
{
	AI_Output (other, self, "DIA_Addon_Cavalorn_Ornament_15_00"); //Szukasz czegoœ?
	AI_Output (self, other, "DIA_Addon_Cavalorn_Ornament_08_01"); //To takie oczywiste? Tak, Magowie Wody kazali mi szukaæ zgubionych ornamentów.
	AI_Output (other, self, "DIA_Addon_Cavalorn_Ornament_15_02"); //Interesuj¹cy zbieg okolicznoœci. Otrzyma³em taki sam rozkaz od Nefariusa.
	AI_Output (self, other, "DIA_Addon_Cavalorn_Ornament_08_06"); //To dobrze. W takim razie nie muszê tego robiæ sam.
	AI_Output (other, self, "DIA_Addon_Cavalorn_Ornament_15_07"); //Znalaz³eœ ju¿ jakiœ ornament?
	AI_Output (self, other, "DIA_Addon_Cavalorn_Ornament_08_08"); //Znam miejsce, w którym powinniœmy coœ znaleŸæ.
	AI_Output (self, other, "DIA_Addon_Cavalorn_Ornament_08_09"); //Ten kamienny kr¹g mo¿e byæ jedn¹ z tych budowli, które opisa³ Nefarius.
	AI_Output (self, other, "DIA_Addon_Cavalorn_Ornament_08_10"); //Teraz musimy tylko znaleŸæ mechanizm.
};

//******************************************************************************************
//	Info Triggered
//******************************************************************************************
instance DIA_Addon_Cavalorn_Triggered (C_INFO)
{
	npc									=	Bau_4300_Addon_Cavalorn;
	nr									=	5;
	condition							=	DIA_Addon_Cavalorn_Triggered_Condition;
	information							=	DIA_Addon_Cavalorn_Triggered_Info;
	description							=	"Uruchomi³em mechanizm. Nic siê nie sta³o.";
};

func int DIA_Addon_Cavalorn_Triggered_Condition()
{
	if (MIS_Addon_Nefarius_BringMissingOrnaments == LOG_RUNNING)
	&& (Npc_KnowsInfo(other,DIA_Addon_Cavalorn_Ornament))
	&& (ORNAMENT_SWITCHED_FARM == true)
	{
		return true;
	};
};

func void DIA_Addon_Cavalorn_Triggered_Info()
{
	AI_Output (other, self, "DIA_Addon_Cavalorn_Triggered_15_00"); //Uruchomi³em mechanizm. Nic siê nie sta³o.
	AI_Output (self, other, "DIA_Addon_Cavalorn_Triggered_08_01"); //W takim razie wieœniacy Lobarta mieli racjê.
	AI_Output (self, other, "DIA_Addon_Cavalorn_Triggered_08_02"); //Powiedzieli, ¿e ktoœ ju¿ krêci³ siê przy tych kamieniach.
	AI_Output (self, other, "DIA_Addon_Cavalorn_Triggered_08_03"); //Nagle znik¹d pojawi³ siê kamienny stra¿nik i ich zaatakowa³.
	AI_Output (self, other, "DIA_Addon_Cavalorn_Triggered_08_04"); //Farmerzy wezwali stra¿, ¿o³nierze paladynów, a ci przyszli i zniszczyli potwora.
	AI_Output (self, other, "DIA_Addon_Cavalorn_Triggered_08_05"); //Przeszuka³em ju¿ ca³y teren. Je¿eli by³ tu jakiœ ornament, maj¹ go teraz paladyni.

	Info_ClearChoices(DIA_Addon_Cavalorn_Triggered);
	Info_AddChoice (DIA_Addon_Cavalorn_Triggered, "Jeden z nas musi pójœæ do górnego miasta.", DIA_Addon_Cavalorn_Triggered_Pal);
	Info_AddChoice (DIA_Addon_Cavalorn_Triggered, "W takim razie pójdê do nich i odzyskam ornament.", DIA_Addon_Cavalorn_Triggered_OBack);
};

func void B_Cavalorn_Triggered_Wohin()
{
	AI_UnequipArmor(self);
	CreateInvItem (self, ITAR_LEATHER_L);
	Npc_RemoveInvItems (self, ITAR_WaterRanger, Npc_HasItems(self,ITAR_WaterRanger));
	AI_EquipBestArmor(self);
	
	AI_Output (self, other, "DIA_Addon_Cavalorn_Triggered_Wohin_08_00"); //Pójdê do miasta. Spotkamy siê u Vatrasa.
	AI_StopProcessInfos(self);
	Npc_ExchangeRoutine	(self, "Stadt");

	SetNoteEntry (Quest_Ornament, LOG_MISSION, "Jeden z brakuj¹cych fragmentów ornamentu jest w posiadaniu paladynów w górnym mieœcie.");

	MIS_Addon_Cavalorn_GetOrnamentFromPAL = LOG_RUNNING;
	self.flags = 0;
};

func void DIA_Addon_Cavalorn_Triggered_OBack()
{
	AI_Output (other, self, "DIA_Addon_Cavalorn_Triggered_OBack_15_00"); //W takim razie pójdê do nich i odzyskam ornament.
	AI_Output (self, other, "DIA_Addon_Cavalorn_Triggered_OBack_08_01"); //Dobrze.
	B_Cavalorn_Triggered_Wohin();
};

func void DIA_Addon_Cavalorn_Triggered_Pal()
{
	AI_Output (other, self, "DIA_Addon_Cavalorn_Triggered_Pal_15_00"); //Jeden z nas musi pójœæ do górnego miasta.
	AI_Output (self, other, "DIA_Addon_Cavalorn_Triggered_Pal_08_01"); //Nie mam na to czasu. Ty musisz to zrobiæ.
	B_Cavalorn_Triggered_Wohin();
};

//******************************************************************************************
//	Info GotOrnaFromHagen
//******************************************************************************************
instance DIA_Addon_Cavalorn_GotOrnaFromHagen (C_INFO)
{
	npc									=	Bau_4300_Addon_Cavalorn;
	nr									=	5;
	condition							=	DIA_Addon_Cavalorn_GotOrnaFromHagen_Condition;
	information							=	DIA_Addon_Cavalorn_GotOrnaFromHagen_Info;
	description							=	"Dosta³em brakuj¹cy ornament od Lorda Hagena.";
};

func int DIA_Addon_Cavalorn_GotOrnaFromHagen_Condition()
{
	if (Lord_Hagen_GotOrnament == true)
	{
		return true;
	};
};

func void DIA_Addon_Cavalorn_GotOrnaFromHagen_Info()
{
	AI_Output (other, self, "DIA_Addon_Cavalorn_GotOrnaFromHagen_15_00"); //Dosta³em brakuj¹cy ornament od Lorda Hagena.
	AI_Output (self, other, "DIA_Addon_Cavalorn_GotOrnaFromHagen_08_01"); //Widzisz? Wiedzia³em, ¿e mieli go paladyni.
	B_GivePlayerXP(XP_BONUS_1);
	MIS_Addon_Cavalorn_GetOrnamentFromPAL = LOG_SUCCESS;
};

//******************************************************************************************
//	WannaLearn
//******************************************************************************************
instance DIA_Addon_Cavalorn_WannaLearn (C_INFO)
{
	npc									=	BAU_4300_Addon_Cavalorn;
	nr									=	800;
	condition							=	DIA_Addon_Cavalorn_WannaLearn_Condition;
	information							=	DIA_Addon_Cavalorn_WannaLearn_Info;
	description							=	"Mo¿esz mnie czegoœ nauczyæ?";
};

func int DIA_Addon_Cavalorn_WannaLearn_Condition()
{
	if (Npc_KnowsInfo(other,DIA_Addon_Cavalorn_Hallo))
	&& ((Npc_KnowsInfo(other,DIA_Addon_Cavalorn_PCKilledBrago)) || (Npc_KnowsInfo(other,DIA_Addon_Cavalorn_HELFEN)))
	{
		return true;
	};
};

func void DIA_Addon_Cavalorn_WannaLearn_Info()
{
	AI_Output (other, self, "DIA_Addon_Cavalorn_WannaLearn_15_00"); //Mo¿esz mnie czegoœ nauczyæ?
	AI_Output (self, other, "DIA_Addon_Cavalorn_WannaLearn_08_01"); //Pewnie. Przecie¿ wiesz. Oj, ch³opie, nieŸle ciê przegonili.
	AI_Output (self, other, "DIA_Addon_Cavalorn_WannaLearn_08_02"); //Niczego nie pamiêtasz, prawda?

	self.aivar[AIV_CanTeach] = true;
	SetNoteEntry (Note_Teachers, LOG_NOTE, "Cavalorn mo¿e mi pokazaæ, jak korzystaæ z ³uku oraz jak siê skradaæ.");
};
