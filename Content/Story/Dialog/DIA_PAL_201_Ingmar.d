///////////////////////////////////////////////////////////////////////
//	Info EXIT 
///////////////////////////////////////////////////////////////////////
INSTANCE DIA_Ingmar_EXIT   (C_INFO)
{
	npc         = Pal_201_Ingmar;
	nr          = 999;
	condition   = DIA_Ingmar_EXIT_Condition;
	information = DIA_Ingmar_EXIT_Info;
	permanent   = true;
	description = DIALOG_ENDE;
};

FUNC INT DIA_Ingmar_EXIT_Condition()
{
	if (Kapitel < 9)
	{
		return true;
	};
};

FUNC VOID DIA_Ingmar_EXIT_Info()
{
	AI_StopProcessInfos (self);
};

///////////////////////////////////////////////////////////////////////
//	Info GREET
///////////////////////////////////////////////////////////////////////
instance DIA_Ingmar_Hallo		(C_INFO)
{
	npc		 	 = 	Pal_201_Ingmar;
	nr			 =  2;
	condition	 = 	DIA_Ingmar_Hallo_Condition;
	information	 = 	DIA_Ingmar_Hallo_Info;
	permanent    =  true;
	important	 = 	true;
};
//------------------------------------
var int DIA_Ingmar_Hallo_permanent;
//------------------------------------
func int DIA_Ingmar_Hallo_Condition ()
{	
	if Npc_IsInState (self, ZS_Talk)
	&& (DIA_Ingmar_Hallo_permanent == false)
	&& (Kapitel < 10)
	{
		return true;
	};
};
func void DIA_Ingmar_Hallo_Info ()
{
	if (EnterOW_Kapitel2 == false)
	&& (LordHagen.aivar[AIV_TalkedToPlayer] == true)
	{ 
		AI_Output (self, other, "DIA_Ingmar_Hallo_06_00"); //Z otrzymanych przeze mnie raportów wynika, ¿e Górnicza Dolina to niebezpieczne miejsce.
		AI_Output (self, other, "DIA_Ingmar_Hallo_06_01"); //Pamiêtaj, aby zabraæ ze sob¹ odpowiednie wyposa¿enie.
	}
	else if (MIS_OLDWORLD == LOG_SUCCESS)
	&& 		(LordHagen.aivar[AIV_TalkedToPlayer] == true)
	{
		AI_Output (self, other, "DIA_Ingmar_Hallo_06_02"); //Sytuacja w Górniczej Dolinie jest doœæ niepokoj¹ca. Potrzebny nam plan, dziêki któremu za¿egnane zostanie niebezpieczeñstwo, a nasi ch³opcy wróc¹ do domu wraz z wydobyt¹ rud¹.
		DIA_Ingmar_Hallo_permanent = true;
	}
	else 
	{
		AI_Output (self, other, "DIA_Ingmar_Hallo_06_03"); //S¹dzi³em, ¿e przyszed³eœ porozmawiaæ z Lordem Hagenem. Na co czekasz?
	};
};
///////////////////////////////////////////////////////////////////////
//	Info Krieg mit den Orks 
///////////////////////////////////////////////////////////////////////
instance DIA_Ingmar_Krieg		(C_INFO)
{
	npc		 	 = 	Pal_201_Ingmar;
	nr			 =  99;
	condition	 = 	DIA_Ingmar_Krieg_Condition;
	information	 = 	DIA_Ingmar_Krieg_Info;
	permanent    =  false;
	description	 =  "Jak sytuacja na kontynencie?";
};
func int DIA_Ingmar_Krieg_Condition ()
{	
	if Npc_IsInState (self, ZS_Talk)
	{
		return true;
	};
};
func void DIA_Ingmar_Krieg_Info ()
{
	AI_Output (other, self, "DIA_Ingmar_Krieg_15_00"); //Jak sytuacja na kontynencie?
	AI_Output (self, other, "DIA_Ingmar_Krieg_06_01"); //Jeszcze nie wygraliœmy wojny, chocia¿ królewska armia zmusi³a orków do odwrotu.
	AI_Output (self, other, "DIA_Ingmar_Krieg_06_02"); //Wci¹¿ jednak musimy stawiaæ czo³a niewielkim grupom najeŸdŸców.
	AI_Output (self, other, "DIA_Ingmar_Krieg_06_03"); //Te stworzenia nie znaj¹ honoru ani wiary - dlatego wierzê w nasze ostateczne zwyciêstwo.
};	

//#####################################################################
//##
//##
//##							KAPITEL 3
//##
//##
//#####################################################################

// ************************************************************
// 	  				   EXIT KAP3
// ************************************************************

INSTANCE DIA_Ingmar_KAP3_EXIT(C_INFO)
{
	npc			= Pal_201_Ingmar;
	nr			= 999;
	condition	= DIA_Ingmar_KAP3_EXIT_Condition;
	information	= DIA_Ingmar_KAP3_EXIT_Info;
	permanent	= true;
	description = DIALOG_ENDE;
};                       
FUNC INT DIA_Ingmar_KAP3_EXIT_Condition()
{
	if (Kapitel == 9)	
	{
		return true;
	};
};
FUNC VOID DIA_Ingmar_KAP3_EXIT_Info()
{	
	AI_StopProcessInfos	(self);
};


//#####################################################################
//##
//##
//##							KAPITEL 4
//##
//##
//#####################################################################


// ************************************************************
// 	  				   EXIT KAP4
// ************************************************************

INSTANCE DIA_Ingmar_KAP4_EXIT(C_INFO)
{
	npc			= Pal_201_Ingmar;
	nr			= 999;
	condition	= DIA_Ingmar_KAP4_EXIT_Condition;
	information	= DIA_Ingmar_KAP4_EXIT_Info;
	permanent	= true;
	description = DIALOG_ENDE;
};                       
FUNC INT DIA_Ingmar_KAP4_EXIT_Condition()
{
	if (Kapitel == 10)	
	{
		return true;
	};
};
FUNC VOID DIA_Ingmar_KAP4_EXIT_Info()
{	
	AI_StopProcessInfos	(self);
};

	
///////////////////////////////////////////////////////////////////////
//	Info Orkelite
///////////////////////////////////////////////////////////////////////
instance DIA_Ingmar_ORKELITE		(C_INFO)
{
	npc		 = 	Pal_201_Ingmar;
	nr		 = 	40;
	condition	 = 	DIA_Ingmar_ORKELITE_Condition;
	information	 = 	DIA_Ingmar_ORKELITE_Info;
	description	 = 	"Orkowie planuj¹ ogromny atak.";
};

func int DIA_Ingmar_ORKELITE_Condition ()
{
	if ((TalkedTo_AntiPaladin == true) || (Npc_HasItems (other,ItRi_OrcElite)) || (Hagen_SawOrcRing == true))
	&& (hero.guild == GIL_PAL)
	{
		return true;
	};
};

func void DIA_Ingmar_ORKELITE_Info ()
{

	AI_Output (other, self, "DIA_Ingmar_ORKELITE_15_00"); //Orkowie planuj¹ ogromny atak.
	AI_Output (self, other, "DIA_Ingmar_ORKELITE_06_01"); //Doprawdy? Bardzo interesuj¹ce. A sk¹d niby o tym wiesz?

	if (TalkedTo_AntiPaladin == true)
	{
	AI_Output (other, self, "DIA_Ingmar_ORKELITE_15_02"); //Rozmawia³em z nimi.
	};

	AI_Output (other, self, "DIA_Ingmar_ORKELITE_15_03"); //Niektórzy z ich przywódców grasuj¹ w tej okolicy.
	AI_Output (self, other, "DIA_Ingmar_ORKELITE_06_04"); //Hm. Nie wygl¹da mi to na typow¹ strategiê orków.

	Info_ClearChoices	(DIA_Ingmar_ORKELITE);
	Info_AddChoice	(DIA_Ingmar_ORKELITE, "Lepiej wymyœl, jak siê ich pozbyæ.", DIA_Ingmar_ORKELITE_loswerden );
	Info_AddChoice	(DIA_Ingmar_ORKELITE, "Co teraz zrobimy?", DIA_Ingmar_ORKELITE_wasTun );
	Info_AddChoice	(DIA_Ingmar_ORKELITE, "Co masz na myœli?", DIA_Ingmar_ORKELITE_wieso );

	Log_CreateTopic (TOPIC_OrcElite, LOG_MISSION);
	Log_SetTopicStatus(TOPIC_OrcElite, LOG_RUNNING);
	B_LogEntry (TOPIC_OrcElite,"Ingmara bardzo zainteresowa³a historia o przywódcy hersztów orków."); 

	MIS_KillOrkOberst = LOG_RUNNING;
};
func void DIA_Ingmar_ORKELITE_loswerden ()
{
	AI_Output (other, self, "DIA_Ingmar_ORKELITE_loswerden_15_00"); //Lepiej wymyœl, jak siê ich pozbyæ.
	AI_Output (self, other, "DIA_Ingmar_ORKELITE_loswerden_06_01"); //Gdybyœmy mieli wiêcej informacji... Wyœlê kogoœ na zwiady.
	Info_ClearChoices	(DIA_Ingmar_ORKELITE);
};

func void DIA_Ingmar_ORKELITE_wieso ()
{
	AI_Output (other, self, "DIA_Ingmar_ORKELITE_wieso_15_00"); //Co to oznacza?
	AI_Output (self, other, "DIA_Ingmar_ORKELITE_wieso_06_01"); //A jeœli mówisz prawdê, oznacza to, ¿e próbuj¹ nas najpierw os³abiæ, wysy³aj¹c tu swoich najlepszych wojowników.
	AI_Output (self, other, "DIA_Ingmar_ORKELITE_wieso_06_02"); //Przewa¿nie jeden dowódca prowadzi ca³¹ hordê orkowych wojowników. Zwykle nie spotyka siê ich w grupach.
	AI_Output (self, other, "DIA_Ingmar_ORKELITE_wieso_06_03"); //Taka strategia ma swoje uzasadnienie - przywódcy stanowi¹ rdzeñ si³ szturmowych i zwykle s¹ otoczeni kordonem wojowników.
	AI_Output (self, other, "DIA_Ingmar_ORKELITE_wieso_06_04"); //Dlatego, aby siê do nich zbli¿yæ, trzeba pokonaæ przynajmniej 30 wojowników.
	
	B_LogEntry (TOPIC_OrcElite,"Ingmar wspomina³ coœ o przywódcy hersztów orków."); 
};

func void DIA_Ingmar_ORKELITE_wasTun ()
{
	AI_Output (other, self, "DIA_Ingmar_ORKELITE_wasTun_15_00"); //Co teraz zrobimy?
	AI_Output (self, other, "DIA_Ingmar_ORKELITE_wasTun_06_01"); //Kiedy pojawiaj¹ siê w takiej liczbie, to przewa¿nie tworz¹ patrol prowadzony przez najwy¿szego rang¹ przywódcê.
	AI_Output (self, other, "DIA_Ingmar_ORKELITE_wasTun_06_02"); //Herszt zak³ada siedzibê i wycofuje siê do jakiejœ jaskini, z której wydaje rozkazy swoim oddzia³om.
	AI_Output (self, other, "DIA_Ingmar_ORKELITE_wasTun_06_03"); //Gdyby uda³o nam siê schwytaæ tego przywódcê, zyskalibyœmy ogromn¹ przewagê.
	AI_Output (self, other, "DIA_Ingmar_ORKELITE_wasTun_06_04"); //Orkowi przywódcy lubi¹ przebywaæ blisko wroga. Jego jaskini nale¿y zatem szukaæ gdzieœ w pobli¿u miasta.
	AI_Output (self, other, "DIA_Ingmar_ORKELITE_wasTun_06_05"); //W pobli¿u farmy Lobarta zauwa¿ono kilku orków. Mo¿e w³aœnie tam nale¿a³oby rozpocz¹æ poszukiwania.

	B_LogEntry (TOPIC_OrcElite,"Ingmar s¹dzi, ¿e orkowy przywódca przebywa prawdopodobnie w jaskini gdzieœ w pobli¿u farmy Lobarta. Ingmar chce, abym go odnalaz³ i pokona³."); 
	
	Info_ClearChoices	(DIA_Ingmar_ORKELITE);
};


///////////////////////////////////////////////////////////////////////
//	Info Hauptquartier
///////////////////////////////////////////////////////////////////////
instance DIA_Ingmar_HAUPTQUARTIER		(C_INFO)
{
	npc			 = 	Pal_201_Ingmar;
	nr			 = 	41;
	condition	 = 	DIA_Ingmar_HAUPTQUARTIER_Condition;
	information	 = 	DIA_Ingmar_HAUPTQUARTIER_Info;
	permanent	 =  false;
	description	 = 	"Uda³o mi siê odnaleŸæ siedzibê orków.";
};

func int DIA_Ingmar_HAUPTQUARTIER_Condition ()
{
	if (Npc_IsDead(OrkElite_AntiPaladinOrkOberst))
	&& (Npc_KnowsInfo(other, DIA_Ingmar_ORKELITE))
	{
		return true;
	};
};

func void DIA_Ingmar_HAUPTQUARTIER_Info ()
{
	AI_Output (other, self, "DIA_Ingmar_HAUPTQUARTIER_15_00"); //Uda³o mi siê odnaleŸæ siedzibê orków. Ich herszt poleg³ w walce.
	AI_Output (self, other, "DIA_Ingmar_HAUPTQUARTIER_06_01"); //To wspania³a wiadomoœæ. Przez jakiœ czas w szeregach wroga zapanuje chaos.
	AI_Output (self, other, "DIA_Ingmar_HAUPTQUARTIER_06_02"); //Muszê przyznaæ, ¿e nieŸle siê spisa³eœ. Gdybyœmy mieli wiêcej rycerzy takich jak ty, nadchodz¹ca bitwa by³aby dla nas bu³k¹ z mas³em.
	AI_Output (self, other, "DIA_Ingmar_HAUPTQUARTIER_06_03"); //Proszê, weŸ to z³oto i kup sobie za nie trochê ekwipunku.

	B_GivePlayerXP(XP_BONUS_10);
	CreateInvItems (self, ItMi_Gold, 300);									
	B_GiveInvItems (self, other, ItMi_Gold, 300);					
	MIS_KillOrkOberst = LOG_SUCCESS;
};


//#####################################################################
//##
//##
//##							KAPITEL 5
//##
//##
//#####################################################################

// ************************************************************
// 	  				   EXIT KAP5
// ************************************************************

INSTANCE DIA_Ingmar_KAP5_EXIT(C_INFO)
{
	npc			= Pal_201_Ingmar;
	nr			= 999;
	condition	= DIA_Ingmar_KAP5_EXIT_Condition;
	information	= DIA_Ingmar_KAP5_EXIT_Info;
	permanent	= true;
	description = DIALOG_ENDE;
};                       
FUNC INT DIA_Ingmar_KAP5_EXIT_Condition()
{
	if (Kapitel == 11)	
	{
		return true;
	};
};
FUNC VOID DIA_Ingmar_KAP5_EXIT_Info()
{	
	AI_StopProcessInfos	(self);
};


//#####################################################################
//##
//##
//##							KAPITEL 6
//##
//##
//#####################################################################

// ************************************************************
// 	  				   EXIT KAP6
// ************************************************************


INSTANCE DIA_Ingmar_KAP6_EXIT(C_INFO)
{
	npc			= Pal_201_Ingmar;
	nr			= 999;
	condition	= DIA_Ingmar_KAP6_EXIT_Condition;
	information	= DIA_Ingmar_KAP6_EXIT_Info;
	permanent	= true;
	description = DIALOG_ENDE;
};                       
FUNC INT DIA_Ingmar_KAP6_EXIT_Condition()
{
	if (Kapitel == 12)	
	{
		return true;
	};
};
FUNC VOID DIA_Ingmar_KAP6_EXIT_Info()
{	
	AI_StopProcessInfos	(self);
};
