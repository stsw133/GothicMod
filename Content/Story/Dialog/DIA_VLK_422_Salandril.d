//******************************************************************************************
//	Info EXIT
//******************************************************************************************
INSTANCE DIA_Salandril_EXIT (C_INFO)
{
	npc									=	VLK_422_Salandril;
	nr									=	999;
	condition							=	atrue;
	information							=	DIA_Salandril_EXIT_Info;
	permanent							=	true;
	description							=	DIALOG_ENDE;
};

FUNC VOID DIA_Salandril_EXIT_Info()
{
	AI_StopProcessInfos(self);
};

//******************************************************************************************
//	Info Hallo
//******************************************************************************************
instance DIA_Salandril_Hallo (C_INFO)
{
	npc									=	VLK_422_Salandril;
	nr									=	2;
	condition							=	DIA_Salandril_Hallo_Condition;
	information							=	DIA_Salandril_Hallo_Info;
	permanent							=	false;
	important							=	true;
};

func int DIA_Salandril_Hallo_Condition()
{
	if (Npc_IsInState(self,ZS_Talk))
	&& (QuestStep_OldWorld != LOG_SUCCESS)
	{
		return true;
	};
};

func void DIA_Salandril_Hallo_Info()
{
	AI_Output (self, other, "DIA_Salandril_PERM_13_00"); //Witaj, wêdrowcze. Szukasz doskona³ej mikstury?
	AI_Output (self, other, "DIA_Salandril_PERM_13_01"); //Mam œwietny wybór po rozs¹dnych cenach. A moje mikstury s¹ o wiele lepsze od tych, które stara siê sprzedawaæ Zuris.
	
	SetNoteEntry (Note_Traders, LOG_NOTE, "Salandril sprzedaje mikstury. Jego sklep znajduje siê w górnym mieœcie.");
};

//******************************************************************************************
//	Info Trank
//******************************************************************************************
instance DIA_Salandril_Trank (C_INFO)
{
	npc									=	VLK_422_Salandril;
	nr									=	2;
	condition							=	DIA_Salandril_Trank_Condition;
	information							=	DIA_Salandril_Trank_Info;
	permanent							=	false;
	important							=	true;
};

func int DIA_Salandril_Trank_Condition()
{
	if (Npc_IsInState(self,ZS_Talk))
	&& (QuestStep_OldWorld == LOG_SUCCESS)
	&& (Npc_KnowsInfo(other,DIA_Salandril_KLOSTER) == false)
	{
		return true;
	};
};

func void DIA_Salandril_Trank_Info()
{
	AI_Output (self, other, "DIA_Salandril_Trank_13_00"); //S³ysza³em, ¿e by³eœ z paladynami w Górniczej Dolinie. Jestem pod wra¿eniem.
	AI_Output (self, other, "DIA_Salandril_Trank_13_01"); //Nie œpiesz siê i spokojnie obejrzyj mój towar. Dobrze siê sk³ada, bo akurat mam coœ wyj¹tkowego na sprzeda¿.
	CreateInvItems (self, ItPo_Perm_DEX, 1);
};

//******************************************************************************************
//	Info Trade
//******************************************************************************************
instance DIA_Salandril_Trade (C_INFO)
{
	npc									=	VLK_422_Salandril;
	nr									=	9;
	condition							=	DIA_Salandril_Trade_Condition;
	information							=	DIA_Salandril_Trade_Info;
	permanent							=	true;
	description							=	"Poka¿ mi swoje towary..";
	trade								=	true;
};

func int DIA_Salandril_Trade_Condition()
{
	if (Npc_KnowsInfo(other,DIA_Salandril_KLOSTER) == false)
	{
		return true;
	};
};

func void DIA_Salandril_Trade_Info()
{
	B_GiveTradeInv(self);
	AI_Output (other, self, "DIA_Salandril_Trade_15_00"); //Poka¿ mi swoje towary.
	
	if (other.guild == GIL_KDF)
	{
		AI_Output (self, other, "DIA_Salandril_Trade_13_01"); //Z przyjemnoœci¹, czcigodny Bracie.
		if (QuestStep_FakeMineDocs == LOG_RUNNING)
		{
			SC_KnowsProspektorSalandril = true;
		};
	};
	if (other.guild == GIL_PAL)
	{
		AI_Output (self, other, "DIA_Salandril_Trade_13_02"); //Z przyjemnoœci¹, szlachetny Panie.
	};
};

//******************************************************************************************
//	Info Kloster
//******************************************************************************************
instance DIA_Salandril_KLOSTER (C_INFO)
{
	npc									=	VLK_422_Salandril;
	nr									=	2;
	condition							=	DIA_Salandril_KLOSTER_Condition;
	information							=	DIA_Salandril_KLOSTER_Info;
	permanent							=	false;
	description							=	"Masz udaæ siê do klasztoru, w którym zostaniesz os¹dzony!";
};

func int DIA_Salandril_KLOSTER_Condition()
{
	if ((SC_KnowsProspektorSalandril == true) || (QuestStep_SerpentesBringSalandril == LOG_RUNNING))
	{
		return true;
	};
};

func void DIA_Salandril_KLOSTER_Info()
{
	AI_Output (other, self, "DIA_Salandril_KLOSTER_15_00"); //Masz udaæ siê do klasztoru, w którym zostaniesz os¹dzony!
	AI_Output (self, other, "DIA_Salandril_KLOSTER_13_01"); //Co? Nie ma mowy! Ci nêdzni magowie nie maj¹ nawet cienia dowodu przeciwko mnie!

	if (hero.guild == GIL_KDF)
	&& (SC_KnowsProspektorSalandril == true)
	{
		AI_Output (other, self, "DIA_Salandril_KLOSTER_15_02"); //A co z tymi fa³szywymi udzia³ami w kopalni, które mo¿na wszêdzie kupiæ? Widnieje na nich twój podpis. Jesteœ winny!
	}
	else
	{
		AI_Output (other, self, "DIA_Salandril_KLOSTER_15_03"); //Mam rozkazy do wykonania i albo ze mn¹ pójdziesz, albo ciê do tego zmuszê.
	};
	
	AI_Output (self, other, "DIA_Salandril_KLOSTER_13_04"); //CO? Przeci¹gnê ciê przez ca³e miasto i poka¿ê, jak nale¿y siê do mnie zwracaæ!
	
	AI_StopProcessInfos(self);
	B_Attack (self, other, AR_NONE, 1);
};

//******************************************************************************************
//	Info GehinsKloster
//******************************************************************************************
instance DIA_Salandril_GehinsKloster (C_INFO)
{
	npc									=	VLK_422_Salandril;
	nr									=	2;
	condition							=	DIA_Salandril_GehinsKloster_Condition;
	information							=	DIA_Salandril_GehinsKloster_Info;
	permanent							=	false;
	description							=	"No, to teraz pójdziesz do klasztoru, czy bêdziemy siê dalej bawiæ?";
};

func int DIA_Salandril_GehinsKloster_Condition()
{
	if ((SC_KnowsProspektorSalandril == true) || (QuestStep_SerpentesBringSalandril == LOG_RUNNING))
	&& (self.aivar[AIV_LastFightAgainstPlayer] == FIGHT_LOST)
	&& (Npc_KnowsInfo(other,DIA_Salandril_KLOSTER))
	{
		return true;
	};
};

func void DIA_Salandril_GehinsKloster_Info()
{
	AI_Output (other, self, "DIA_Salandril_GehinsKloster_15_00"); //No, to teraz pójdziesz do klasztoru, czy bêdziemy siê dalej bawiæ?
	AI_Output (self, other, "DIA_Salandril_GehinsKloster_13_01"); //Bêdziesz tego ¿a³owa³! Cholera! Pójdê do tego twojego klasztoru, ale nie myœl, ¿e ujdzie ci to na sucho.
	
	QuestStep_SerpentesBringSalandril = SetQuestStatus (Quest_SerpentesBringSalandril, LOG_SUCCESS, QuestStep_SerpentesBringSalandril);
	
	AI_StopProcessInfos(self);
	Npc_ExchangeRoutine	(self, "KlosterUrteil");
};

//******************************************************************************************
//	Info Verschwinde
//******************************************************************************************
instance DIA_Salandril_Verschwinde (C_INFO)
{
	npc									=	VLK_422_Salandril;
	nr									=	2;
	condition							=	DIA_Salandril_Verschwinde_Condition;
	information							=	DIA_Salandril_Verschwinde_Info;
	permanent							=	true;
	important							=	true;
};

func int DIA_Salandril_Verschwinde_Condition()
{
	if (QuestStep_SerpentesBringSalandril == LOG_SUCCESS)
	&& (Npc_IsInState(self,ZS_Talk))
	{
		return true;
	};	
};

func void DIA_Salandril_Verschwinde_Info()
{
	B_Say (self, other, "$NOTNOW");
	AI_StopProcessInfos(self);
};
